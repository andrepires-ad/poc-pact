buildscript {
	ext {
		lombokVersion = '1.16.20'
	}
	repositories {
		mavenCentral()
	}
	dependencies {
		classpath("io.swagger:swagger-codegen:2.2.2")
		classpath("org.springframework.boot:spring-boot-gradle-plugin:1.5.8.RELEASE")
		classpath group: 'au.com.dius', name: 'pact-jvm-provider-gradle_2.12', version: '3.5.14'
	}
}

plugins {
	id 'java'
	id 'idea'
	id "au.com.dius.pact" version "3.5.14"
}

apply plugin: 'org.springframework.boot'
apply plugin: 'maven-publish'

group = 'com.appdirect.pact.provider'
version = '0.1.0'

dependencyManagement {
	imports {
		mavenBom("org.springframework.cloud:spring-cloud-dependencies:Dalston.SR4")
	}
}

def getDockerTags() {
	def tags = []
	tags << project.version.toString()

	if (!project.version.toString().endsWith("-SNAPSHOT")) {
		// if it is not a SNAPSHOT, then also add the 'latest' tag
		tags << "latest"
	}
	return tags
}

ext {
	prNumber = project.properties['PR_NUMBER']
	pactBrokerTag = prNumber ? prNumber + "SNAPSHOT" : "master"
	dockerRegistry = "docker.appdirect.tools"
	artifactVersion = project.version.toString()
	dockerName = "${dockerRegistry}/appdirect-pact-sample/provider-service"
	dockerTags = getDockerTags()

	pactTest = project.properties['PACT_TEST'] ? project.properties['PACT_TEST'] : "false"
	pactBrokerHost = "192.168.64.16"
	pactBrokerPort = 32033
}

configurations {
	generatedCompile
}

sourceCompatibility = 1.8
targetCompatibility = 1.8
dependencies {
	compile("org.springframework.boot:spring-boot-starter-web")
	compile("org.springframework.boot:spring-boot-starter-actuator")
	compile('net.logstash.logback:logstash-logback-encoder:3.5')

	// test dependencies
	testCompile("org.springframework.boot:spring-boot-starter-test")

	testCompile "org.springframework.boot:spring-boot-starter-test"
	testCompile "org.springframework.boot:spring-boot"
	testCompile group: 'junit', name: 'junit', version:'4.12'
	testCompile group: 'au.com.dius', name: 'pact-jvm-provider-spring_2.12', version: '3.5.14'

	generatedCompile 'org.springframework.boot:spring-boot-starter-data-rest:2.0.1.RELEASE'
	generatedCompile 'io.springfox:springfox-swagger2:2.5.0'
	generatedCompile 'io.springfox:springfox-swagger-ui:2.5.0'

}

tasks.withType(JavaCompile) {
	options.encoding = 'UTF-8'
}

repositories {
	mavenLocal()
	maven { url "http://repo.maven.apache.org/maven2" }
	jcenter()
}

sourceSets {
	generated {
		compileClasspath = configurations.generatedCompile
	}
	main {
		compileClasspath += generated.output
		runtimeClasspath += generated.output
	}
	test {
		compileClasspath += generated.output
		runtimeClasspath += generated.output
	}
}

apply from: 'build.docker.gradle'
apply from: 'build.swagger.gradle'
//apply from: 'build.pact.gradle'
apply from: 'build.springboot.gradle'
apply from: 'build.publish.gradle'

