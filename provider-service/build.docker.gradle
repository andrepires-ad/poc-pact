
/**
 * Task to build the docker image
 */
task dockerBuild {
	description "Task to build the docker image. Uses the docker tag defined on the root project. See dockerTag ext."
	dependsOn build
	doLast {
		def buildArgs = [
				"build", "--build-arg", "ARTIFACT_VERSION=${artifactVersion}"
		]
		dockerTags.each {
			tag ->
				buildArgs << "-t"
				buildArgs << "${dockerName}:${tag}"
		}
		buildArgs << "."
		exec {
			executable 'docker'
			args buildArgs
		}
	}
}

/**
 * Task to push the docker image. Requires access to the docker registry.
 */
task dockerPush {
	description "Task to push the docker image to the docker repository. Requires access to the docker registry. Uses the docker tag defined on the root project. See dockerTag ext."
	dependsOn dockerBuild
	doLast {
		dockerTags.each {
			tag ->
				exec {
					executable 'docker'
					args 'push', "${dockerName}:${tag}"
				}
		}
	}
}


def containerName = "provider-service-container"

task dockerRunDaemon {
	dependsOn dockerBuild
	doLast {
		def buildArgs = [
				"run", "--name", containerName, "-d", "-p", "9090:9090", "${dockerName}:${artifactVersion}"
		]
		exec {
			executable 'docker'
			args buildArgs
		}
	}
}

task dockerStop {
	doLast {
		exec {
			executable 'docker'
			args "stop", containerName
		}
		exec {
			executable 'docker'
			args "rm", containerName
		}
	}
}