
// PACT
def appHost = "localhost"
def appPort = "9090"


// This will be called before the provider task
task startTheApp {
	dependsOn "dockerRunDaemon"
	doLast {
		// wait until container is ready to receive requests
		def ready = false
		def maxTryCount = 30
		def tryCount = 0
		while (!ready && tryCount < maxTryCount) {
			tryCount++
			try {
				Thread.sleep(1000)
				def connection = new URL("http://${appHost}:${appPort}/health").openConnection() as HttpURLConnection
				connection.setRequestProperty( 'Accept', 'application/json' )
				if (connection.responseCode == 200) {
					ready = true
				}
			} catch (Exception e) {
				print "*"
			}
		}
	}
}


// This will be called after the provider task
task killTheApp {
	dependsOn "dockerStop"
}

pact {
	serviceProviders {
		"provider-service" {
			startProviderTask = startTheApp
			terminateProviderTask = killTheApp
			stateChangeUrl = url("http://${appHost}:${appPort}/pactcontracts/states")
			protocol = 'http'
			host = appHost
			port = appPort
			hasPactsFromPactBroker("http://" + pactBrokerHost + ":" + pactBrokerPort)
		}
	}
}

